plugins {
	id 'org.springframework.boot' version '2.4.4'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'jacoco'
    id "com.github.spacialcircumstances.gradle-cucumber-reporting" version "0.1.22"
	id 'idea'
}

group = 'bdd.demo.'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

def versions = [
		openapi: '1.5.4',
		cucumber: '6.9.1'
]

sourceSets {
	testInt {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
}

idea {
	module {
		testSourceDirs += project.sourceSets.testInt.java.srcDirs
		testSourceDirs += project.sourceSets.testInt.resources.srcDirs
	}
}

configurations {
	compileOnly.extendsFrom annotationProcessor
	testIntImplementation.extendsFrom implementation
	testIntRuntimeOnly.extendsFrom runtimeOnly
}

repositories {
	mavenCentral()
}

dependencies {
	annotationProcessor 'org.projectlombok:lombok'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	runtimeOnly 'com.h2database:h2'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	testIntImplementation 'org.springframework.boot:spring-boot-starter-test'
	testIntImplementation "io.cucumber:cucumber-java:${versions.cucumber}"
	testIntImplementation "io.cucumber:cucumber-junit-platform-engine:${versions.cucumber}"
}

task testInt(type: Test) {
	description = 'Runs integration tests.'
	group = 'verification'

	testClassesDirs = sourceSets.testInt.output.classesDirs
	classpath = sourceSets.testInt.runtimeClasspath
	shouldRunAfter test
}

tasks.withType(Test) {
	useJUnitPlatform()
}

jacocoTestReport {
	// The JaCoCo plugin adds a JacocoTaskExtension extension to all tasks of type Test.
	// Use task state to include or not task execution data
	// https://docs.gradle.org/current/javadoc/org/gradle/api/tasks/TaskState.html
	// This declaration will be used as a closure, notice there no wrapping parenthesis
	//executionData tasks.withType(Test).findAll { it.state.executed }

	//executionData fileTree(project.buildDir.absolutePath).include("jacoco/*.exec")

	// https://github.com/gradle/gradle/issues/11034
	// wrap the query in a closure, so that it is deferred until the task starts
	//executionData { tasks.withType(Test).findAll { it.state.executed }.jacoco.destinationFile }

	// works
	executionData(test, testInt)


	reports {
		xml.enabled = true
		// HTML report: build/reports/jacoco/test/html/index.html
		html.enabled = true
	}
}

cucumberReports {
    buildId = '0'
    outputDir = file("${buildDir}/reports/cucumber")
    reports = files("${buildDir}/reports/cucumber/cucumber-report.json")
}

check.dependsOn testInt, jacocoTestReport